' Gambas class file

Private conector As Connection
Private Contenido As Result
Private Busqueda As Result
Private FilaSeleccionada As Integer

Public Sub btnCerrarColeccion_Click()

  ' ' Desconectar la base de datos
  ' If conector Then
  '   conector.Close()
  '   conector = Null
  ' Endif
  Me.Close()

End

Public Sub Form_Open()

  ' Crear una nueva conexión
  conector = New Connection

  ' Configurar los parámetros de conexión
  conector.Type = "sqlite3"
  conector.Host = File.Dir(FMain.TextBoxRuta.Text)
  conector.Name = "gbtexpublisher.sqlite"

  ' Intentar abrir la conexión
  Try conector.Open()

  If Error Then
    conector = Null
    Message.Error("Error al intentar conectarse con la base de datos. Asegúrese de que el archivo de la base de datos <strong>gbtexpublisher.sqlite</strong> se encuentra disponible. El mismo debe encontrarse en " & User.Home & "/.gbtexpublisher")
  Else

    btnGuardarColeccion.Visible = False
    btnGuardarCambiosColeccion.Visible = False

    Mostrar_Lista()
  Endif

End

Public Sub Mostrar_Lista() 'Función que rellena el GridView

  Dim Record As String
  Dim Count As Integer
  Dim i As Integer

  GridJournal.Clear()

  Contenido = conector.Exec("select * from journal ORDER BY id DESC")

  'diseñamos el tableview
  GridJournal.Columns.Count = 7
  GridJournal.Header = True
  GridJournal.Columns[0].Title = "id"
  GridJournal.Columns[0].Width = 0
  GridJournal.Columns[1].Title = "Revista"
  GridJournal.Columns[1].Width = 250
  GridJournal.Columns[2].Title = "ISSN"
  GridJournal.Columns[2].Width = 120
  GridJournal.Columns[3].Title = "eISSN"
  GridJournal.Columns[3].Width = 120
  GridJournal.Columns[4].Title = "email"
  GridJournal.Columns[4].Width = 450
  GridJournal.Columns[5].Title = "website"
  GridJournal.Columns[5].Width = 0
  GridJournal.Columns[6].Title = "DOI"
  GridJournal.Columns[6].Width = 0

  'ASIGNO LA CANTIDAD DE FILAS SEGUN LA CANTIDAD DE DATOS QUE EXISTEN
  GridJournal.Rows.Count = Contenido.Count

  'recorremos el resutado de la consulta para mostrarlo
  For i = 0 To Contenido.Count - 1
    GridJournal[i, 0].Text = Contenido!id
    GridJournal[i, 1].Text = Contenido!nombre
    GridJournal[i, 2].Text = Contenido!issn
    GridJournal[i, 3].Text = Contenido!eissn
    GridJournal[i, 4].Text = Contenido!email
    GridJournal[i, 5].Text = Contenido!website
    GridJournal[i, 6].Text = Contenido!doi

    Contenido.MoveNext
  Next

  Contenido = conector.Exec("SELECT COUNT(*) FROM journal")

  Record = Contenido[0]
  Count = Record

End

Public Sub BtnNuevoColeccion_Click()

  BtnNuevoColeccion.Visible = False
  btnGuardarColeccion.Visible = True
  btnGuardarCambiosColeccion.visible = False

  Dim Idn As Integer

  LimpiarCampos  'Limpiamos todos los campos

  ' Obtenemos el último ID y sumamos +1 para el nuevo registro
  Contenido = conector.Exec("SELECT MAX(id) FROM journal LIMIT 1")

  ' Si no hay ninguna entrada entonces arrancamos desde 0
  If Contenido["MAX(id)"] = Null Then
    Idn = 0
  Else
    Idn = CInt(Contenido["MAX(id)"]) + 1
  Endif
  txtID.Text = Idn
  Mostrar_Lista
  txtRevista.SetFocus()

End

Public Function LimpiarCampos()

  'Limpiamos los campos
  txtID.Text = ""
  txtRevista.Text = ""
  txtISSN.Text = ""
  txteISSN.Text = ""
  txtEmail.Text = ""
  txtWebsite.Text = ""
  txtDOI.Text = ""

End

Public Sub btnGuardarColeccion_Click()

  Dim Comando As String

  ' Escapa los caracteres especiales(comilla simple)en el título
  Dim tituloEscapado As String

  tituloEscapado = Replace(txtRevista.Text, "'", "''")

  eliminarBlancos

  Comando = "SELECT * FROM journal WHERE nombre = '" & tituloEscapado & "'"
  Busqueda = conector.Exec(Comando)

  ' Verificar si se encontraron resultados en la consulta
  If Busqueda.Count > 0 Then
    Message.Warning("La colección que desea agregar ya existe en la base de datos.")
    Return ' Salir de la función si el título ya existe
  Endif

  Contenido = conector.Create("journal")

  guardarCampos

  Contenido.Update
  Message.Info("Nueva revista guardada.")

  Mostrar_Lista ' Refrescamos la lista de entradas
  LimpiarCampos ' Limpiamos los campos

  btnGuardarCambiosColeccion.Visible = False
  btnGuardarColeccion.Visible = False
  BtnNuevoColeccion.Visible = True

End

Public Function eliminarBlancos()

  EliminarEspaciosAlInicioYFinal(txtRevista)
  EliminarEspaciosAlInicioYFinal(txtISSN)
  EliminarEspaciosAlInicioYFinal(txteISSN)
  EliminarEspaciosAlInicioYFinal(txtEmail)
  EliminarEspaciosAlInicioYFinal(txtWebsite)
  EliminarEspaciosAlInicioYFinal(txtDOI)

End

Public Sub EliminarEspaciosAlInicioYFinal(textbox As TextBox)

  Dim texto As String

  texto = textbox.Text
  texto = Trim$(texto)
  textbox.Text = texto

End

Public Function guardarCampos()

  Contenido!id = txtID.Text
  Contenido!nombre = txtRevista.Text
  Contenido!issn = txtISSN.Text
  Contenido!eissn = txteISSN.Text
  Contenido!email = txtEmail.Text
  Contenido!website = txtWebsite.Text
  Contenido!doi = txtDOI.Text

End

Public Sub GridJournal_RowClick(Row As Integer)

  BtnNuevoColeccion.Visible = False
  btnGuardarCambiosColeccion.visible = True
  btnGuardarColeccion.Visible = False

  txtID.Text = GridJournal[Row, 0].Text
  txtRevista.Text = GridJournal[Row, 1].Text
  txtISSN.Text = GridJournal[Row, 2].Text
  txteISSN.Text = GridJournal[Row, 3].Text
  txtEmail.Text = GridJournal[Row, 4].Text
  txtWebsite.Text = GridJournal[Row, 5].Text
  txtDOI.Text = GridJournal[Row, 6].Text

  FilaSeleccionada = Row

  ' Ahora habilita el botón después de confirmar que hay una fila seleccionada.
  btnSeleccion.Visible = True

End

Public Sub btnGuardarCambiosColeccion_Click()

  BtnNuevoColeccion.Visible = True
  btnGuardarColeccion.Visible = False
  btnGuardarCambiosColeccion.visible = False

  'eliminamos blancos de inicio y final de cadena en los textbox
  eliminarBlancos

  Contenido = conector.Edit("journal", "id=" & txtID.Text)
  guardarCampos
  Contenido.Update
  Message.Info("Los cambios se guardaron con éxito.")

  Mostrar_Lista
  LimpiarCampos 'limpiar campos

End

Public Sub btnSeleccion_Click()

  Dim revistaSeleccionada As String = GridJournal[FilaSeleccionada, 1].Text
  Dim issnSeleccionada As String = GridJournal[FilaSeleccionada, 2].Text
  Dim eissnSeleccionada As String = GridJournal[FilaSeleccionada, 3].Text
  Dim webSeleccionada As String = GridJournal[FilaSeleccionada, 5].Text
  Dim doiSeleccionada As String = GridJournal[FilaSeleccionada, 6].Text

  ' Verificar si hay una fila seleccionada.
  If FilaSeleccionada >= 0 Then
    FMain.txtPublication.Text = revistaSeleccionada
    FMain.txt36_pdfissn.Text = issnSeleccionada
    FMain.txt37_pdfeissn.Text = eissnSeleccionada
    FMain.txtBaseURL.Text = webSeleccionada
    FMain.txt85_doiRevista.Text = doiSeleccionada
    FMain.txt35_pdfisbn.Clear
    Me.Close()
    ' Desconectar la base de datos
    If conector Then
      conector.Close()
      conector = Null
    Endif
  Else
    Message.Warning("Debe seleccionar una colección.")
  Endif

End

Public Sub Form_Close()

  ' Desconectar la base de datos
  If conector Then
    conector.Close()
    conector = Null
  Endif

End
